import Quickshell
import Quickshell.Wayland
import Quickshell.Io
import QtQuick
import QtQuick.Layouts

ShellRoot {
    id: root

    // Theme colors
    property color colBg: "#1a1b26"
    property color colFg: "#a9b1d6"
    property color colMuted: "#444b6a"
    property color colCyan: "#0db9d7"
    property color colPurple: "#ad8ee6"
    property color colRed: "#f7768e"
    property color colYellow: "#e0af68"
    property color colBlue: "#7aa2f7"
    property color colGreen: "#9ece6a"
    property color colOrange: "#ff9e64"
    property color colPink: "#f2cdcd"

    // Font
    property string fontFamily: "JetBrainsMono Nerd Font"
    property int fontSize: 14

    // System info properties
    property int cpuUsage: 0
    property int memUsage: 0
    property int diskUsage: 0
    property int volumeLevel: 0
    property string activeWindow: "Window"
    property string currentLayout: "Tile"
    property int focusedWorkspace: 1
    property var occupiedWorkspaces: []
    property int batteryLevel: 0
    property string networkStatus: "Down"
    property string systemUptime: "0m"

    // CPU tracking
    property var lastCpuIdle: 0
    property var lastCpuTotal: 0

    // CPU usage
    Process {
        id: cpuProc
        command: ["sh", "-c", "head -1 /proc/stat"]
        stdout: SplitParser {
            onRead: data => {
                if (!data) return
                var parts = data.trim().split(/\s+/)
                var user = parseInt(parts[1]) || 0
                var nice = parseInt(parts[2]) || 0
                var system = parseInt(parts[3]) || 0
                var idle = parseInt(parts[4]) || 0
                var iowait = parseInt(parts[5]) || 0
                var irq = parseInt(parts[6]) || 0
                var softirq = parseInt(parts[7]) || 0

                var total = user + nice + system + idle + iowait + irq + softirq
                var idleTime = idle + iowait

                if (lastCpuTotal > 0) {
                    var totalDiff = total - lastCpuTotal
                    var idleDiff = idleTime - lastCpuIdle
                    if (totalDiff > 0) {
                        cpuUsage = Math.round(100 * (totalDiff - idleDiff) / totalDiff)
                    }
                }
                lastCpuTotal = total
                lastCpuIdle = idleTime
            }
        }
        Component.onCompleted: running = true
    }

    // Memory usage
    Process {
        id: memProc
        command: ["sh", "-c", "free | grep Mem"]
        stdout: SplitParser {
            onRead: data => {
                if (!data) return
                var parts = data.trim().split(/\s+/)
                var total = parseInt(parts[1]) || 1
                var used = parseInt(parts[2]) || 0
                memUsage = Math.round(100 * used / total)
            }
        }
        Component.onCompleted: running = true
    }

    // Disk usage
    Process {
        id: diskProc
        command: ["sh", "-c", "df / | tail -1"]
        stdout: SplitParser {
            onRead: data => {
                if (!data) return
                var parts = data.trim().split(/\s+/)
                var percentStr = parts[4] || "0%"
                diskUsage = parseInt(percentStr.replace('%', '')) || 0
            }
        }
        Component.onCompleted: running = true
    }

    // Volume level (wpctl for PipeWire)
    Process {
        id: volProc
        command: ["wpctl", "get-volume", "@DEFAULT_AUDIO_SINK@"]
        stdout: SplitParser {
            onRead: data => {
                if (!data) return
                var match = data.match(/Volume:\s*([\d.]+)/)
                if (match) {
                    volumeLevel = Math.round(parseFloat(match[1]) * 100)
                }
            }
        }
        Component.onCompleted: running = true
    }

    // Active window title (i3)
    Process {
        id: windowProc
        command: ["sh", "-c", "i3-msg -t get_tree | jq -r '.. | select(.focused? == true and .window != null) | .name // empty' | head -1"]
        stdout: SplitParser {
            onRead: data => {
                if (data && data.trim()) {
                    activeWindow = data.trim()
                }
            }
        }
        Component.onCompleted: running = true
    }

    // Current layout (i3: splith, splitv, tabbed, stacked)
    Process {
        id: layoutProc
        command: ["sh", "-c", "i3-msg -t get_tree | jq -r '.. | select(.focused? == true) | .layout // empty' | head -1"]
        stdout: SplitParser {
            onRead: data => {
                if (data && data.trim()) {
                    var layout = data.trim()
                    if (layout === "splith") {
                        currentLayout = "Horizontal"
                    } else if (layout === "splitv") {
                        currentLayout = "Vertical"
                    } else if (layout === "tabbed") {
                        currentLayout = "Tabbed"
                    } else if (layout === "stacked" || layout === "stacking") {
                        currentLayout = "Stacking"
                    } else if (layout === "output" || layout === "none") {
                        currentLayout = "Tiled"
                    } else {
                        currentLayout = layout.charAt(0).toUpperCase() + layout.slice(1)
                    }
                }
            }
        }
        Component.onCompleted: running = true
    }

    // Focused workspace (i3) — normalise 0 to 1 so bar shows 1–10
    Process {
        id: workspaceProc
        command: ["sh", "-c", "i3-msg -t get_workspaces | jq -r '.[] | select(.focused == true) | .num'"]
        stdout: SplitParser {
            onRead: data => {
                if (data && data.trim()) {
                    var num = parseInt(data.trim(), 10)
                    if (!isNaN(num)) {
                        focusedWorkspace = (num === 0) ? 1 : num
                    }
                }
            }
        }
        Component.onCompleted: running = true
    }

    // Occupied workspaces (i3) — one-line output, normalise 0 to 1 so bar shows 1–10
    Process {
        id: occupiedProc
        command: ["sh", "-c", "i3-msg -t get_workspaces | jq -r '[.[].num] | join(\" \")'"]
        stdout: SplitParser {
            onRead: data => {
                if (data && data.trim()) {
                    var parts = data.trim().split(/\s+/)
                    var nums = []
                    for (var i = 0; i < parts.length; i++) {
                        var v = parseInt(parts[i], 10)
                        if (!isNaN(v)) {
                            v = (v === 0) ? 1 : v
                            if (nums.indexOf(v) === -1) nums.push(v)
                        }
                    }
                    occupiedWorkspaces = nums
                }
            }
        }
        Component.onCompleted: running = true
    }

    // Battery status
    Process {
        id: batteryProc
        command: ["cat", "/sys/class/power_supply/BAT0/capacity"]
        stdout: SplitParser {
            onRead: data => {
                if (data) batteryLevel = parseInt(data.trim()) || 0
            }
        }
        Component.onCompleted: running = true
    }

    // Network connection status
    Process {
        id: networkProc
        command: ["sh", "-c", "cat /sys/class/net/*/operstate | grep -m1 up || echo down"]
        stdout: SplitParser {
            onRead: data => {
                if (data && data.trim() === "up") {
                    networkStatus = "Up"
                } else {
                    networkStatus = "Down"
                }
            }
        }
        Component.onCompleted: running = true
    }

    // System uptime
    Process {
        id: uptimeProc
        command: ["sh", "-c", "fastfetch | grep 'Uptime:' | sed 's/Uptime: //' | sed 's/\x1b\\[[0-9;]*[A-Za-z]//g'"]
        stdout: SplitParser {
            onRead: data => {
                if (data) systemUptime = data.trim()
            }
        }
        Component.onCompleted: running = true
    }

    

    // Slow timer for system stats
    Timer {
        interval: 200
        running: true
        repeat: true
        onTriggered: {
            cpuProc.running = true
            memProc.running = true
            diskProc.running = true
            volProc.running = true
            batteryProc.running = true
            networkProc.running = true
        }
    }

    // Medium timer for uptime
    Timer {
        interval: 1000
        running: true
        repeat: true
        onTriggered: uptimeProc.running = true
    }

    // Fast timer for window/layout/workspace (i3)
    Timer {
        interval: 200
        running: true
        repeat: true
        onTriggered: {
            windowProc.running = true
            layoutProc.running = true
            workspaceProc.running = true
            occupiedProc.running = true
        }
    }

    Variants {
        model: Quickshell.screens

        PanelWindow {
            property var modelData
            screen: modelData

            anchors {
                top: true
                left: true
                right: true
            }

            implicitHeight: 30
            color: root.colBg

            margins {
                top: 0
                bottom: 0
                left: 0
                right: 0
            }

            Rectangle {
                anchors.fill: parent
                color: root.colBg

                RowLayout {
                    anchors.fill: parent
                    spacing: 0

                    Item { width: 8 }

                    Rectangle {
                        Layout.preferredWidth: 24
                        Layout.preferredHeight: 24
                        color: "transparent"

                        Image {
                            anchors.fill: parent
                            source: "file:///home/tony/.config/quickshell/icons/tonybtw.png"
                            fillMode: Image.PreserveAspectFit
                        }
                    }

                    Item { width: 8 }

                    Repeater {
                        model: [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]

                        Rectangle {
                            Layout.preferredWidth: 20
                            Layout.preferredHeight: parent.height
                            color: "transparent"

                            property int workspaceNum: modelData
                            property bool isActive: focusedWorkspace === workspaceNum
                            property bool hasWindows: occupiedWorkspaces.indexOf(workspaceNum) !== -1

                            Text {
                                text: workspaceNum
                                color: parent.isActive ? root.colCyan : (parent.hasWindows ? root.colCyan : root.colMuted)
                                font.pixelSize: root.fontSize
                                font.family: root.fontFamily
                                font.bold: true
                                anchors.centerIn: parent
                            }

                            Rectangle {
                                width: 20
                                height: 3
                                color: parent.isActive ? root.colPurple : root.colBg
                                anchors.horizontalCenter: parent.horizontalCenter
                                anchors.bottom: parent.bottom
                            }

                            MouseArea {
                                anchors.fill: parent
                                onClicked: {
                                    var proc = Qt.createQmlObject('import Quickshell.Io; Process { }', parent)
                                    proc.command = ["i3-msg", "workspace", "number", String(workspaceNum)]
                                    proc.running = true
                                }
                            }
                        }
                    }

                    Rectangle {
                        Layout.preferredWidth: 1
                        Layout.preferredHeight: 16
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 8
                        Layout.rightMargin: 8
                        color: root.colMuted
                    }

                    

                    Text {
                        text: activeWindow
                        color: root.colPurple
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.fillWidth: true
                        Layout.leftMargin: 8
                        elide: Text.ElideRight
                        maximumLineCount: 1
                    }

                    Text {
                        text: "CPU: " + cpuUsage + "%"
                        color: root.colYellow
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.rightMargin: 8
                    }

                    Rectangle {
                        Layout.preferredWidth: 1
                        Layout.preferredHeight: 16
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 0
                        Layout.rightMargin: 8
                        color: root.colMuted
                    }

                    Text {
                        text: "Mem: " + memUsage + "%"
                        color: root.colCyan
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.rightMargin: 8
                    }

                    Rectangle {
                        Layout.preferredWidth: 1
                        Layout.preferredHeight: 16
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 0
                        Layout.rightMargin: 8
                        color: root.colMuted
                    }

                    Text {
                        text: "Disk: " + diskUsage + "%"
                        color: root.colBlue
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.rightMargin: 8
                    }

                    Rectangle {
                        Layout.preferredWidth: 1
                        Layout.preferredHeight: 16
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 0
                        Layout.rightMargin: 8
                        color: root.colMuted
                    }

                    Text {
                        text: "Vol: " + volumeLevel + "%"
                        color: root.colPurple
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.rightMargin: 8
                    }

                    Rectangle {
                        Layout.preferredWidth: 1
                        Layout.preferredHeight: 16
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 0
                        Layout.rightMargin: 8
                        color: root.colMuted
                    }

                    Text {
                        text: "Bat: " + batteryLevel + "%"
                        color: batteryLevel > 40 ? root.colGreen : root.colRed
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.rightMargin: 8
                    }

                    Rectangle {
                        Layout.preferredWidth: 1
                        Layout.preferredHeight: 16
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 0
                        Layout.rightMargin: 8
                        color: root.colMuted
                    }

                    Text {
                        text: "Net: " + networkStatus
                        color: networkStatus === "Up" ? root.colOrange : root.colMuted
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.rightMargin: 8
                    }

                    Rectangle {
                        Layout.preferredWidth: 1
                        Layout.preferredHeight: 16
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 0
                        Layout.rightMargin: 8
                        color: root.colMuted
                    }

                    Text {
                        text: "Up: " + systemUptime
                        color: root.colFg
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.rightMargin: 8
                    }

                    Rectangle {
                        Layout.preferredWidth: 1
                        Layout.preferredHeight: 16
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 0
                        Layout.rightMargin: 8
                        color: root.colMuted
                    }

                    Text {
                        id: clockText
                        text: Qt.formatDateTime(new Date(), "ddd, MMM dd - HH:mm")
                        color: root.colPink
                        font.pixelSize: root.fontSize
                        font.family: root.fontFamily
                        font.bold: true
                        Layout.rightMargin: 8

                        Timer {
                            interval: 1000
                            running: true
                            repeat: true
                            onTriggered: clockText.text = Qt.formatDateTime(new Date(), "ddd, MMM dd - HH:mm")
                        }
                    }

                    Item { width: 8 }
                }
            }
        }
    }
}
